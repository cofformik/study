P8255A EQU 0640H
P8255B EQU 0642H
P8255C EQU 0646H
P8255MODE EQU 0646H
SSTACK	SEGMENT STACK
		DW 32 DUP(0)
SSTACK	ENDS
 
CODE   	SEGMENT
	   	ASSUME CS:CODE
 
START: 	MOV DX,P8255MODE
		MOV AL,10010000B
		OUT DX,AL				;送方式控制字
NEXT:
		MOV AL,0FFH				;数据灯全亮
		
		MOV DX,P8255B
		OUT DX,AL				;送b口
		
		PUSH DS
		MOV AX, 0000H
		MOV DS, AX
		MOV AX, OFFSET MIR7		;取中断入口地址
		MOV SI, 003CH			;中断矢量地址
		MOV [SI], AX			;填IRQ7的偏移矢量
		MOV AX, CS				;段地址
		MOV SI, 003EH
		MOV [SI], AX			;填IRQ7的段地址矢量		
		
		MOV AX, OFFSET MIR6			
		MOV SI, 0038H
		MOV [SI], AX
		MOV AX, CS
		MOV SI, 003AH
		MOV [SI], AX
		CLI						;初始化前要关中断
		POP DS
		;初始化主片8259
		MOV AL, 11H				;级联，边沿触发，要ICW4
		OUT 20H, AL				;ICW1
		MOV AL, 08H				;中断类型号从8开始
		OUT 21H, AL				;ICW2
		MOV AL, 04H				;
		OUT 21H, AL				;ICW3
		MOV AL, 01H				;非缓冲方式，8086/8088配置
		OUT 21H, AL				;ICW4
 
		;初始化从片8259
		MOV AL, 11H				;级联，边沿触发，要ICW4
		OUT 0A0H, AL			;ICW1
		MOV AL, 30H				;中断类型号从30H开始
		OUT 0A1H, AL			;ICW2
		MOV AL, 02H				;通过IR1引脚连接主片
		OUT 0A1H, AL			;ICW3
		MOV AL, 01H				;非缓冲方式，8086/8088配置
		OUT 0A1H, AL			;ICW4
		MOV AL, 0FDH
		OUT 0A1H,AL				;从8259 OCW1 = 1111 1101	允许IR1中断请求	
		MOV AL, 2BH       		;0010 1011
		OUT 21H, AL				;主8259 OCW1	不屏蔽IR2 IR4 IR6 IR7
		STI
 
AA2:	NOP
		MOV AL,0FFH				;数据灯全亮
		MOV DX,P8255B
		OUT DX,AL				;送b口
		JMP AA2
 
MIR7:
		MOV AL,0F0H				;红灯亮（绿灯灭）数据灯高四位为红灯
		MOV DX,P8255B
		OUT DX,AL				;送b口
		MOV AL, 20H
		OUT 20H, AL				;中断结束命令
		CALL DELAY
		IRET
MIR6:
		MOV AL,00FH				;绿灯亮（红灯灭）数据灯低四位为绿灯
		MOV DX,P8255B
		OUT DX,AL				;送b口
		MOV AL, 20H
		OUT 20H, AL				;中断结束命令
		CALL DELAY
		IRET
		
DELAY:	PUSH CX					;延时子程序
		MOV AL, 05H
AA1:	MOV CX, 0FFFFH	
AA0:	PUSH AX
		POP  AX
		LOOP AA0
		SUB AL,1
		JNZ AA1
		POP CX
		RET
CODE	ENDS
		END  START